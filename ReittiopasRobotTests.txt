*** Settings ***
Library   Selenium2Library
Library   String

Suite Setup   Open Browser   ${STARTPAGE}   ${BROWSER}
Test Setup    Go to   ${STARTPAGE}
Suite Teardown   Close All Browsers
Documentation    Testing routeplanner Reittiopas.fi 

*** Variables ***
${STARTPAGE}   http://www.reittiopas.fi
${BROWSER}   firefox
${GETROUTEBUTTON}   SearchFormSubmitButton
${BICYCLEBUTTON}   cyclingTripplannerButton
${DEPARTURE RADIOBUTTON}   time_method1
${ARRIVAL RADIOBUTTON}   time_method2
${TRANSPORTATION XPATH}   /html/body/div[1]/div[2]/div/div/div[2]/div[1]/div[1]/table/tbody/tr[7]/td[3]/div/table/tbody/tr/td[2]/a

*** Test Cases ***
Set route from Alaportti 4 to Iso Omena at 12:00 and find eligible route
   Set route from-to   Alaportti 4   Iso Omena
   Set clock   12:00
   Submit form and wait results   ${GETROUTEBUTTON}
   Check that suggested route contains   Alaportti 4, Espoo
   Check correct transportation line   ${TRANSPORTATION XPATH}   Bussi 533
   Check that suggested route contains   Iso Omena, Espoo
   
Set route from Matinraitti 4 to Vanha Maantie 6 at rush hour and find eligible route
   Set route from-to   Matinraitti 4   Vanha Maantie 6
   Set clock   8:00
   Set time to represent departure/arrival   ${ARRIVAL RADIOBUTTON}
   Submit form and wait results   ${GETROUTEBUTTON}
   Check that suggested route contains   Matinraitti 4, Espoo
   Check correct transportation line   ${TRANSPORTATION XPATH}   Bussi 5
   Check that suggested route contains   Vanha Maantie 6, Espoo

Set route from Matinraitti 4 to Vanha Maantie 6 not at rush hour and find eligible route
   Set route from-to   Matinraitti 4   Vanha Maantie 6
   set clock   12:00
   Set time to represent departure/arrival   ${DEPARTURE RADIOBUTTON}
   Submit form and wait results   ${GETROUTEBUTTON}
   Check that suggested route contains   Matinraitti 4, Espoo
   Check that suggested route contains   Bussi 132
   Check that suggested route contains   Bussi 550
   Check that suggested route contains   Vanha Maantie 6, Espoo

*** Keywords ***
Set route from-to
   [Arguments]   ${from}   ${to}
   Input Text   id=from_in   ${from}
   Input Text   id=to_in     ${to}
   
Set clock
   [Arguments]   ${time}
   ${hour} =   Fetch From Left   ${time}   :
   ${minute} =   Fetch From Right   ${time}   :
   Input Text      id=hour   ${hour}
   Input Text      name=minute   ${minute}
   
Set time to represent departure/arrival
   [Arguments]    ${radiobutton}
   Click Element   id=${radiobutton}
   
Submit form and wait results
   [Arguments]   ${button}
   Click Element   id=${button}
   Wait Until Page Contains Element   id=ResultDetails
   
Check that suggested route contains
   [Arguments]   ${result}
   Element Should Contain   id=ResultDetails1   ${result}
   
Check correct transportation line
   [Arguments]   ${element xpath}   ${result}
   Element Should Contain   xpath=${element xpath}   ${result}
   
   
   
   
   
   
   
   
   
   