*** Settings ***
Library   Selenium2Library
Library   String

Suite Setup   Open Browser   ${STARTPAGE}   ${BROWSER}
Test Setup    Go to   ${STARTPAGE}
Suite Teardown   Close All Browsers
Documentation    Testing routeplanner Reittiopas.fi features

*** Variables ***
${STARTPAGE}   http://www.reittiopas.fi
${BROWSER}   firefox
${TRANSPORTATION XPATH}   /html/body/div[1]/div[2]/div/div/div[2]/div[1]/div[1]/table/tbody/tr[7]/td[3]/div/table/tbody/tr/td[2]/a
${WEEKDAY}   2014-10-27
${FROM}   Puolarintie 2
${TO}   Saanatunturintie 3

*** Test Cases ***
Find route from to now
   [Tags]   now
   Set route from-to   ${FROM}   ${TO}
   Submit route information and wait results
   Check that result details contains   ${FROM}
   Check that result details contains   ${TO}
   
Set route from Alaportti 4 to Iso Omena at 15:00 and find Bussi 533
   [Tags]   today
   Set route from-to   Alaportti 4   Iso Omena
   Set time   15:00
   Submit route information and wait results
   Check that result details contains   Alaportti 4, Espoo
   Verify desired transportation line   Bussi 533
   Check that result details contains   Iso Omena, Espoo
   
Find route from Matinraitti 4 to Kamppi arrive before 15:00 and find Bussi 132
   [Tags]   today   arrival   seutu
   Set route from-to   Matinraitti 4   Kamppi
   Set time   15:00
   Set time to represent arrival
   Submit route information and wait results
   Check that result details contains   Matinraitti 4, Espoo
   Verify desired transportation line   Bussi 132
   Check that result details contains   Kamppi, Helsinki
   
Set route from Matinraitti 4 to Vanha Maantie 6 at 8:00 and find Bussi 5
   [Tags]   weekday   rush hour
   Set route from-to   Matinraitti 4   Vanha Maantie 6
   Set time   8:00
   Set date of travel   ${WEEKDAY}
   Submit route information and wait results
   Check that result details contains   Matinraitti 4, Espoo
   Verify desired transportation line   Bussi 5
   Check that result details contains   Vanha Maantie 6, Espoo

Set route from Matinraitti 4 to Vanha Maantie 6 at 12:13 and find Bussi 132 and Bussi 550
   [Tags]   weekday   Off-peak time
   Set route from-to   Matinraitti 4   Vanha Maantie 6
   Set time   12:13
   Set date of travel  ${WEEKDAY}
   Submit route information and wait results
   Check that result details contains   Matinraitti 4, Espoo
   Verify desired transportation line   Bussi 132
   Check that result details contains   Bussi 550
   Check that result details contains   Vanha Maantie 6, Espoo

*** Keywords ***
Set route from-to
   [Arguments]   ${from}   ${to}
   Input Text   id=from_in   ${from}
   Input Text   id=to_in     ${to}
   
Set time
   [Arguments]   ${time}
   [Documentation]   Time should be given in hh:mm format
   ${hour} =   Fetch From Left   ${time}   :
   ${minute} =   Fetch From Right   ${time}   :
   Input Text      id=hour   ${hour}
   Input Text      name=minute   ${minute}
   
Set time to represent arrival
   Click Element   id=time_method2
   
Set date of travel
   [Arguments]   ${date}
   [Documentation]   Date should be given in YYYY-MM-DD format
   Click Element   id=hsl_cal_button
   Click Element   id=day_cell_${date}
   
Submit route information and wait results
   Click Element   id=SearchFormSubmitButton
   Wait Until Page Contains Element   id=ResultDetails
   
Check that result details contains
   [Arguments]   ${string}
   [Documentation]   Checks whole result details for string and matches it with the first matching string
   Element Should Contain   id=ResultDetails1   ${string}
   
Verify desired transportation line
   [Arguments]   ${line}
   [Documentation]   Verifies the _first_ means of transport
   Element Should Contain   xpath=${TRANSPORTATION XPATH}   ${line}

   
   
   
   
   